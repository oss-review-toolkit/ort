/*
 * Copyright (C) 2017-2019 HERE Europe B.V.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 * License-Filename: LICENSE
 */

import groovy.transform.ToString
import groovy.transform.TupleConstructor

import javax.inject.Inject

import org.gradle.api.internal.artifacts.repositories.DefaultFlatDirArtifactRepository
import org.gradle.api.internal.artifacts.repositories.DefaultIvyArtifactRepository
import org.gradle.api.internal.artifacts.repositories.DefaultMavenArtifactRepository
import org.gradle.tooling.provider.model.ToolingModelBuilder
import org.gradle.tooling.provider.model.ToolingModelBuilderRegistry

def maxMemInMib = Runtime.getRuntime().maxMemory() / (1024 * 1024)
logger.quiet("Gradle may use a maximum of $maxMemInMib MiB of memory.")

String.metaClass.isAtLeastVersion = { Integer major, Integer minor ->
    def digits = delegate.split('\\.')
    digits[0].toInteger() > major || (digits[0].toInteger() == major && digits[1].toInteger() >= minor)
}

// Apply one of the plugins defined below to register a custom model for the Tooling API. The plugin is required to get
// an instance of ToolingModelBuilderRegistry by dependency injection. Prior to Gradle version 2.14 (see [1]), the
// ToolingModelBuilderRegistry was project-scoped.
//
// [1] https://github.com/gradle/gradle/commit/e9e1b6029a056a5323311f7ee56b216cba6ede63
if (gradle.gradleVersion.isAtLeastVersion(2, 14)) {
    apply plugin: DependencyTreeGradlePlugin
} else {
    apply plugin: DependencyTreeProjectPlugin
}

// The following three interfaces have to match the interfaces defined in "analyzer/src/main/kotlin/GradleSupport.kt"
// because they are used to deserialize the model consumed there.

interface DependencyTreeModel {
    String getGroup()
    String getName()
    String getVersion()
    List<Configuration> getConfigurations()
    List<String> getRepositories()
    List<String> getErrors()
    List<String> getWarnings()
}

interface Configuration {
    String getName()
    List<Dependency> getDependencies()
}

interface Dependency {
    String getGroupId()
    String getArtifactId()
    String getVersion()
    String getClassifier()
    String getExtension()
    List<Dependency> getDependencies()
    String getError()
    String getWarning()
    String getPomFile()
    String getLocalPath()
}

@ToString(includeNames = true)
@TupleConstructor
class DependencyTreeModelImpl implements DependencyTreeModel, Serializable {
    String group
    String name
    String version
    List<Configuration> configurations
    List<String> repositories
    List<String> errors
    List<String> warnings
}

@ToString(includeNames = true)
@TupleConstructor
class ConfigurationImpl implements Configuration, Serializable {
    String name
    List<Dependency> dependencies
}

@ToString(includeNames = true)
@TupleConstructor
class DependencyImpl implements Dependency, Serializable {
    String groupId = ''
    String artifactId = ''
    String version = ''
    String classifier = ''
    String extension = ''
    List<Dependency> dependencies = []
    String error
    String warning
    String pomFile
    String localPath
}

class DependencyTreeGradlePlugin extends AbstractDependencyTreePlugin<Gradle> {
    @Inject
    DependencyTreeGradlePlugin(ToolingModelBuilderRegistry registry) {
        super(registry)
    }
}

class DependencyTreeProjectPlugin extends AbstractDependencyTreePlugin<Project> {
    @Inject
    DependencyTreeProjectPlugin(ToolingModelBuilderRegistry registry) {
        super(registry)
    }
}

class AbstractDependencyTreePlugin<T> implements Plugin<T> {
    private final ToolingModelBuilderRegistry registry

    AbstractDependencyTreePlugin(ToolingModelBuilderRegistry registry) {
        this.registry = registry
    }

    void apply(T t) {
        registry.register(new DependencyTreeModelBuilder())
    }

    private static class DependencyTreeModelBuilder implements ToolingModelBuilder {
        /**
         * Stores the Dependency objects created by this builder, so that they can be reused when the same
         * dependency is encountered again in the dependency graph. As dependencies can occur many times in large
         * dependency graphs, de-duplicating these objects can save a significant amount of memory.
         *
         * Note, however, that packages can be referenced in the graph multiple times with a different set of
         * dependencies. Therefore, for a package with a given identifier, there can be different Dependency
         * objects; hence the values of the map are lists.
         */
        private final Map<String, List<Dependency>> dependencies = new HashMap<>()

        @Override
        boolean canBuild(String modelName) {
            return modelName == 'DependencyTreeModel'
        }

        @Override
        Object buildAll(String modelName, Project project) {
            List<String> errors = []
            List<String> warnings = []

            def gradleVersion = project.gradle.gradleVersion
            if (!gradleVersion.isAtLeastVersion(2, 14)) {
                def error = ("This project uses the unsupported Gradle version $gradleVersion. At least Gradle 2.14 " +
                        'is required.').toString()
                return new DependencyTreeModelImpl(project.group.toString(), project.name, project.version.toString(),
                        [], [], [error], [])
            }

            List<Configuration> configurations = project.configurations.findResults { configuration ->
                if (canBeResolved(configuration)) {
                    ResolutionResult result = configuration.getIncoming().getResolutionResult()
                    Set<ResolvedArtifact> resolvedArtifacts = []

                    try {
                        resolvedArtifacts = configuration.resolvedConfiguration.lenientConfiguration
                                .getArtifacts(Specs.<org.gradle.api.artifacts.Dependency> satisfyAll())
                    } catch (ResolveException e) {
                        project.logger.info("Artifacts for configuration '${configuration.name}' could not be " +
                                "resolved, therefore no information about artifact classifiers and extensions is " +
                                "available: ${e.message}")
                    }

                    List<Dependency> dependencies = result.getRoot().getDependencies().findResults {
                        fetchDependency(it, project, resolvedArtifacts, [] as Set<String>)
                    }

                    new ConfigurationImpl(configuration.name, dependencies)
                } else {
                    project.logger.info("Configuration '${configuration.name}' cannot be resolved.")
                    null
                }
            }

            List<String> repositories = project.repositories.findResults {
                if (it instanceof DefaultMavenArtifactRepository) {
                    it.url.toString()
                } else if (it instanceof DefaultFlatDirArtifactRepository) {
                    warnings.add('Project uses a flat dir repository which is not supported by the analyzer. ' +
                            "Dependencies from this repository will be ignored: ${it.dirs}".toString())
                    null
                } else if (it instanceof DefaultIvyArtifactRepository) {
                    warnings.add('Project uses an Ivy repository which is not supported by the analyzer. ' +
                            "Dependencies from this repository will be ignored: ${it.url}".toString())
                    null
                } else {
                    errors.add("Unknown repository type: ${it.getClass().name}".toString())
                    null
                }
            }

            def version = project.version.toString()
            if (version == 'unspecified') version = ''
            return new DependencyTreeModelImpl(project.group.toString(), project.name, version, configurations,
                    repositories, errors.unique(), warnings.unique())
        }

        /**
         * Returns a Dependency for the given DependencyResult. The function checks whether there is already a
         * Dependency instance in the cache compatible with the result. If this is not the case, parseDependency()
         * is called to generate a new instance.
         *
         * @param dependencyResult represents the package to be processed
         * @param project the current project
         * @param resolvedArtifacts the set of resolved artifacts
         * @param visited a set with dependency nodes already visited to detect cycles in the graph
         * @return the Dependency representing this result
         */
        private Dependency fetchDependency(DependencyResult dependencyResult, Project project,
                                           Set<ResolvedArtifact> resolvedArtifacts, Set<String> visited) {
            // Ignore this dependency if it is a BOM imported from Maven, because BOMs do not define dependencies but
            // version constraints.
            // See: https://docs.gradle.org/current/userguide/platforms.html#sub:bom_import
            if (isBom(dependencyResult)) return null

            def dependencyId = identifierFor(dependencyResult, project)
            dependencies.putIfAbsent(dependencyId, [])
            def dependency = dependencies[dependencyId].find {
                dependencyTreeEquals(it, dependencyResult, project, [] as Set<String>)
            }

            if (dependency != null) {
                return dependency
            }

            dependency = parseDependency(dependencyResult, project, resolvedArtifacts, visited)
            dependencies[dependencyId].add(dependency)
            return dependency
        }

        /**
         * Return true if the provided DependencyResult represents an imported Maven BOM.
         */
        private boolean isBom(DependencyResult dependencyResult) {
            if (dependencyResult instanceof ResolvedDependencyResult &&
                    dependencyResult.selected.hasProperty('variants')) {
                return dependencyResult.selected.variants.every {
                    def categoryAttribute = it.attributes.keySet().find { it.name == 'org.gradle.category' }
                    if (categoryAttribute != null) {
                        def value = it.attributes.getAttribute(categoryAttribute)
                        return value == 'platform' || value == 'enforced-platform'
                    }

                    false
                }
            }

            return false
        }

        /**
         * Creates a new Dependency object to represent the given DependencyResult. The different types of
         * dependencies are evaluated. The (transitive) dependencies of this dependency are processed recursively.
         *
         * @param dependencyResult represents the package to be processed
         * @param project the current project
         * @param resolvedArtifacts the set of resolved artifacts
         * @param parents a set with dependency nodes already visited to detect cycles in the graph
         * @return the newly created Dependency
         */
        private Dependency parseDependency(DependencyResult dependencyResult, Project project,
                                           Set<ResolvedArtifact> resolvedArtifacts, Set<String> parents) {
            if (dependencyResult instanceof ResolvedDependencyResult) {
                List<Dependency> dependencies = dependencyResult.selected.dependencies.findResults { dependency ->
                    // Do not follow circular dependencies, these can exist for project dependencies.
                    if (!(dependencyResult.requested.displayName in parents)) {
                        fetchDependency(dependency, project, resolvedArtifacts,
                                parents + dependencyResult.requested.displayName)
                    } else {
                        null
                    }
                }

                ComponentIdentifier id = dependencyResult.selected.id
                if (id instanceof ModuleComponentIdentifier) {
                    def resolvedComponents = project.dependencies.createArtifactResolutionQuery()
                            .forComponents(id)
                            .withArtifacts(MavenModule, MavenPomArtifact)
                            .execute()
                            .resolvedComponents

                    // Imitate Kotlin's "firstOrNull()".
                    def result = resolvedComponents?.find { true }?.getArtifacts(MavenPomArtifact)?.find { true }

                    String error = null
                    String warning = null
                    String pomFile = null

                    if (result instanceof ResolvedArtifactResult) {
                        pomFile = result.file.absolutePath
                    } else if (result instanceof UnresolvedArtifactResult) {
                        error = collectCauses(result.failure).toString()
                    } else if (result == null) {
                        error = 'Resolution did not return any artifacts'
                    } else {
                        error = "Unknown ArtifactResult type: ${result.getClass().name}".toString()
                    }

                    def artifact = resolvedArtifacts.find {
                        // Cannot use instanceof because the classes below do not exist in all Gradle versions.
                        if (it.owner.getClass().name == 'org.gradle.api.internal.artifacts.DefaultModuleVersionIdentifier') {
                            id.group == it.owner.id.group && id.module == it.owner.id.name && id.version == it.owner.version
                        } else if (it.owner.getClass().name == 'org.gradle.api.internal.artifacts.ivyservice.dynamicversions.DefaultResolvedModuleVersion') {
                            id.group == it.owner.id.group && id.module == it.owner.id.name && id.version == it.owner.id.version
                        } else {
                            error = "Unknown artifact owner type: ${it.owner.getClass().name}".toString()
                            false
                        }
                    }

                    def classifier = artifact?.classifier ?: ''
                    def extension = artifact?.extension ?: ''

                    return new DependencyImpl(id.group, id.module, id.version, classifier, extension, dependencies,
                            error, warning, pomFile, null)
                } else if (id instanceof ProjectComponentIdentifier) {
                    def dependencyProject = project.rootProject.findProject(id.projectPath)
                    return new DependencyImpl(groupId: dependencyProject.group.toString(),
                            artifactId: dependencyProject.name, version: dependencyProject.version.toString(),
                            dependencies: dependencies, localPath: dependencyProject.projectDir.absolutePath)
                } else {
                    return dependencyFromDisplayName(id.displayName, dependencies,
                            "Unknown id type: ${id.getClass().simpleName}", null)
                }
            } else if (dependencyResult instanceof UnresolvedDependencyResult) {
                return dependencyFromDisplayName(dependencyResult.attempted.displayName, [],
                        "Unresolved: ${collectCauses(dependencyResult.failure)}", null)
            } else {
                return dependencyFromDisplayName(dependencyResult.requested.displayName, [],
                        "Unknown result type: ${dependencyResult.getClass().simpleName}", null)
            }
        }

        private static boolean canBeResolved(org.gradle.api.artifacts.Configuration configuration) {
            // The canBeResolved property is available since Gradle 3.3.
            boolean canBeResolved = !configuration.hasProperty('canBeResolved') || configuration.canBeResolved

            // The function getResolutionAlternatives() which belongs to the DeprecatableConfiguration class is
            // available since Gradle 6.0.
            boolean isDeprecatedForResolving = false
            if (configuration.metaClass.respondsTo(configuration, 'getResolutionAlternatives')) {
                isDeprecatedForResolving = configuration.getResolutionAlternatives() != null
            }

            return canBeResolved && !isDeprecatedForResolving
        }

        private static String collectCauses(Throwable throwable) {
            def result = "${throwable.getClass().simpleName}: ${throwable.message}"
            def cause = throwable.cause
            while (cause != null) {
                result += "\nCaused by: ${cause.getClass().simpleName}: ${cause.message}"
                cause = cause.cause
            }
            return result
        }

        /**
         * Checks whether the given dependency and the dependency result spawn the same dependency trees. This is
         * used to test whether the dependency result can be represented by this dependency.
         *
         * @param dependency the dependency
         * @param dependencyResult the dependency result
         * @param project the current project
         * @param visited stores the name of already visited dependencies to deal with cycles
         * @return a flag whether the dependency trees are equal
         */
        private static boolean dependencyTreeEquals(Dependency dependency, DependencyResult dependencyResult,
                                                    Project project, Set<String> visited) {
            def displayName = dependencyResult.requested.displayName
            if (displayName in visited) {
                return true  // We visited this node already.
            }

            def resultDependencies = dependencyResult instanceof ResolvedDependencyResult ?
                    dependencyResult.selected.dependencies : [] as Set<DependencyResult>
            if (dependency.dependencies.size() != resultDependencies.size()) {
                return false
            }

            def dependencyMap = dependency.dependencies.collectEntries { [identifierFor(it), it] }
            def resultMap = resultDependencies.collectEntries { [identifierFor(it, project), it] }
            def nextVisited = visited + displayName

            return dependencyMap.every { entry ->
                def matchingResult = resultMap[entry.key]
                matchingResult != null && dependencyTreeEquals(entry.value as Dependency,
                        matchingResult as DependencyResult, project, nextVisited)
            }
        }

        /**
         * Generates a unique string identifier for a DependencyResult. This is used to de-duplicate dependencies.
         *
         * @param dependencyResult the dependency result
         * @param project the current project
         * @return a unique identifier for this dependency
         */
        private static String identifierFor(DependencyResult dependencyResult, Project project) {
            if (dependencyResult instanceof ResolvedDependencyResult) {
                ComponentIdentifier id = dependencyResult.selected.id
                if (id instanceof ModuleComponentIdentifier) {
                    return toIdentifier(id.group, id.module, id.version)
                } else if (id instanceof ProjectComponentIdentifier) {
                    def dependencyProject = project.rootProject.findProject(id.projectPath)
                    return toIdentifier(dependencyProject.group.toString(), dependencyProject.name,
                            dependencyProject.version.toString())
                } else {
                    return identifierFromDisplayName(id.displayName)
                }
            }

            if (dependencyResult instanceof UnresolvedDependencyResult) {
                return identifierFromDisplayName(dependencyResult.attempted.displayName)
            }

            return identifierFromDisplayName(dependencyResult.requested.displayName)
        }

        /**
         * Generates a unique identifier for the given Dependency.
         *
         * @param dependency the Dependency
         * @return the identifier for this Dependency
         */
        private static String identifierFor(Dependency dependency) {
            return toIdentifier(dependency.groupId, dependency.artifactId, dependency.version)
        }

        /**
         * Creates an object to represent the dependency identified by the given display name. This function is
         * called if no valid dependency identifier is available. It therefore tries to extract the coordinates of
         * the dependency from the display name.
         *
         * @param displayName the display name
         * @param dependencies the list of dependencies of the package affected
         * @param error an optional error message
         * @param warning an optional warning message
         * @return the newly created dependency representation
         */
        private static DependencyImpl dependencyFromDisplayName(String displayName, List<Dependency> dependencies,
                                                        String error, String warning) {
            def coordinates = displayNameToCoordinates(displayName)
            return new DependencyImpl(groupId: coordinates[0], artifactId: coordinates[1], version: coordinates[2],
                    dependencies: dependencies, error: error?.toString(), warning: warning?.toString())
        }

        /**
         * Generates a unique string identifier from a display name. The display name is converted to coordinates,
         * from which the identifier can be constructed.
         *
         * @param displayName the display name
         * @return a unique identifier for this display name
         */
        private static String identifierFromDisplayName(String displayName) {
            def coordinates = displayNameToCoordinates(displayName)
            return toIdentifier(coordinates[0], coordinates[1], coordinates[2])
        }

        /**
         * Splits a display name for a component to an array of groupId, artifactId, and version.
         *
         * @param displayName the name to split
         * @return the array with the coordinates (always 3 components)
         */
        private static String[] displayNameToCoordinates(String displayName) {
            if (displayName.startsWith('project :')) {
                def coordinates = displayName.split(':', 2)
                return ['<project>', coordinates[1], '']
            }

            def coordinates = displayName.split(':')

            if (coordinates.length == 3) {
                return coordinates
            }

            return ['<unknown>', displayName.replace(':', '_'), '']
        }

        /**
         * Generate a unique identifier for the component with the given coordinates.
         *
         * @param group the group
         * @param artifact the artifact
         * @param version the version
         * @return the identifier for these coordinates
         */
        private static String toIdentifier(String group, String artifact, String version) {
            return "$group:$artifact:$version"
        }
    }
}
